apply plugin: 'java'
apply plugin: 'distribution'

defaultTasks('distZip')

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
	provided project(':VotingRewardInterfaceProvider')
	provided project(':VotingRewardAPI')
	provided fileTree(dir: 'libs', include: '*.jar')
}

compileJava {
    //enable compilation in a separate daemon process
    options.fork = true

    //enable incremental compilation
    options.incremental = true
    
    // Exclude custom from being compiled
    exclude('**/custom/**')
}

def generalManifest = manifest {
	attributes(
		'Built-By': System.getProperty('user.name'),
		'Implementation-URL': 'https://github.com/UnAfraid/VotingReward')
}

jar {
	archiveName = 'VotingRewardL2JInterface.jar'
	exclude('**/custom/**')
	manifest {
		from(generalManifest)
	}
}


distributions {
	main {
		contents {
			from('dist')
			into('L2J_Server/dist/libs') {
				from(configurations.runtime)
				from(jar)
				exclude('**/custom/**')
				exclude('l2jserver.jar')
				exclude('mmocore.jar')
				exclude('slf4j-*')
			}
			
			into('L2J_DataPack/dist/game/data/scripts') {
				from('src/main/java') {
					include('**/custom/**')
					exclude('**/com/**');
				}
			}
		}
	}
}


